module terms/StringQuotations

imports 

  terms/Terms

sorts

  PPTerm
  StringQuotation
  StringQuotedPart1 StringQuotedChars1 QuotedBracket1 Dollar1
  StringQuotedPart2 StringQuotedChars2 QuotedBracket2 Dollar2
  StringQuotedPart3 StringQuotedChars3 QuotedBracket3 Dollar3
  StringQuotedPart4 StringQuotedChars4 QuotedBracket4 Dollar4
  Padding

syntax
  
  PPTerm-CF = StringQuotation
  
	StringQuotation-CF.StringQuotation1 = "$" "[" Padding StringQuotedPart1* "]"
	StringQuotedPart1-CF.StringEscape1  = Padding "[" LAYOUT?-CF Term-CF LAYOUT?-CF "]"
	StringQuotedPart1-CF.QStr           = StringQuotedChars1-LEX
	StringQuotedPart1-CF.QDollar        = Dollar1-LEX
	StringQuotedPart1-CF.QBr            = "$" QuotedBracket1-LEX "$"
	StringQuotedChars1-LEX              = ~[\[\]\$]+
	QuotedBracket1-LEX                  = [\[\]]
	Dollar1-LEX                         = "$"
	
  StringQuotation-CF.StringQuotation2 = "$" "{" Padding StringQuotedPart2* "}"
  StringQuotedPart2-CF.StringEscape2  =  Padding "{" LAYOUT?-CF Term-CF LAYOUT?-CF "}"
  StringQuotedPart2-CF.QStr           = StringQuotedChars2-LEX
  StringQuotedPart2-CF.QDollar        = Dollar2-LEX
  StringQuotedPart2-CF.QBr            = "$" QuotedBracket2-LEX "$"
  StringQuotedChars2-LEX              = ~[\{\}\$]+
  QuotedBracket2-LEX                  = [\{\}]
  Dollar2-LEX                         = "$"

  StringQuotation-CF.StringQuotation3 = "$" "(" Padding StringQuotedPart3* ")"
  StringQuotedPart3-CF.StringEscape3  = Padding "(" LAYOUT?-CF Term-CF LAYOUT?-CF ")"
  StringQuotedPart3-CF.QStr           = StringQuotedChars3-LEX
  StringQuotedPart3-CF.QDollar        = Dollar3-LEX
  StringQuotedPart3-CF.QBr            = "$" QuotedBracket3-LEX "$"
  StringQuotedChars3-LEX              = ~[\(\)\$]+
  QuotedBracket3-LEX                  = [\(\)]
  Dollar3-LEX                         = "$"

  StringQuotation-CF.StringQuotation4 = "$" "<" Padding StringQuotedPart4* ">"
  StringQuotedPart4-CF.StringEscape4  = Padding "<"  LAYOUT?-CF Term-CF LAYOUT?-CF ">"
  StringQuotedPart4-CF.QStr           = StringQuotedChars4-LEX
  StringQuotedPart4-CF.QDollar        = Dollar4-LEX
  StringQuotedPart4-CF.QBr            = "$" QuotedBracket4-LEX "$"
  StringQuotedChars4-LEX              = ~[\<\>\$]+
  QuotedBracket4-LEX                  = [\<\>]
  Dollar4-LEX                         = "$"
			
	// Padding is a dummy lexical that will contain the indentation prefix of every quotation
	Padding-CF = Padding-LEX 
	Padding-LEX = {indentpadding}
  
lexical restrictions

  StringQuotedChars1 -/- ~[\[\]\$]
  StringQuotedChars2 -/- ~[\{\}\$]
  StringQuotedChars3 -/- ~[\(\)\$]
  StringQuotedChars4 -/- ~[\<\>\$]
  Dollar1 -/- [\[\]].[\$]
  Dollar2 -/- [\{\}].[\$]
  Dollar3 -/- [\(\)].[\$]
  Dollar4 -/- [\<\>].[\$]
